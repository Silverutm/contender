#!/usr/bin/perl

use strict;
use warnings;
use Database;
use feature 'state';

my %status_priorities = (
  'WAITING'       => 0,
  'JUDGING'       => 1,
  'COMPILE-ERROR' => 2,
  'RUNTIME-ERROR' => 3,
  'TIME-LIMIT'    => 4,
  'WRONG-ANSWER'  => 5,
  'OK'            => 6,
);

sub dequeue {
  my $dbh = shift // db->storage->dbh;

  my @result = ();

  my $sth = $dbh->prepare('
    SELECT id,
           problem_id
      FROM submissions
     WHERE status = "WAITING"');

  $dbh->begin_work;

  $sth->execute();
  if (my ($id, $problem_id) = $sth->fetchrow_array) {
    dbh->do('
      UPDATE submissions
         SET status = "JUDGING"
       WHERE id = ?', {}, $id);

    @result = ($id, $problem_id);
  }

  $dbh->commit;

  return @result;
}

while (1) {
  my $dbh = db->storage->dbh;

  if (my ($id, $problem_id) = dequeue($dbh)) {
    my $judge_status = 'OK';
    my $time_limit = 1.00;

    print STDERR "running submission $id for problem $problem_id\n";
    my $results = `./evaluate $id $problem_id 2>/dev/null`;
    for my $result (split /\n/, $results) {
      my ($test, $status, $duration) = split /\t/, $result;
      $duration ||= '0';

      if ($duration > $time_limit) {
        $status = 'TIME-LIMIT';
      }

      $dbh->do('
        REPLACE INTO judgements
                     (submission_id, testcase, status)
              VALUES (?, ?, ?)',
        {}, $id, $test, $status);

      if ($status_priorities{$status} < $status_priorities{$judge_status}) {
        $judge_status = $status;
      }

      print STDERR "\t$result\n";
    }
    print STDERR "\n";

    $dbh->do('
      UPDATE submissions
         SET status = ?
       WHERE id = ?',
      {}, $judge_status, $id);
  }
  else {
    sleep 1;
  }
}
